import JSON;
import Resources;

/*
	The first three points are a triangle.
	The rest is a triangle strip.
*/

class Course {
	field id;
	field name;
	field music;
	field points = [];
	
	// List of render objects
	field roadTriangles = null;
	
	constructor(id) {
		this.id = id;
		json = Resources.readText('levels/' + id + '.json');
		print(json);
		rawData = JSON.parseJson(json);
		this.name = rawData.get('name') ?? "Untitled level";
		this.music = rawData.get('music');
		points = rawData.get('points', []);
		for (i = 0; i + 1 < points.length; i += 2) {
			this.points.add([1.0 * points[i], 1.0 * points[i + 1]]);
		}
	}
	
	function getRoadTriangles() {
		if (this.roadTriangles == null) {
			this.roadTriangles = [];
			for (i = 2; i < this.points.length; ++i) {
				a = this.points[i - 2];
				b = this.points[i - 1];
				c = this.points[i];
				this.roadTriangles.add([
					ObjectType.TRIANGLE,
					[
						(a[0] + b[0] + c[0]) / 3.0,
						(a[1] + b[1] + c[1]) / 3.0,
						0.0,
						0 // sort priority is low.
					],
					[a + [0.0], b + [0.0], c + [0.0]],
					[40, 40, 40],
				]);
			}
		}
		return this.roadTriangles;
	}
}
